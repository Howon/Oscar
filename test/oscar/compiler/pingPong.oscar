message setPing(ping: actor<Ping>)
message setPong(pong: actor<Pong>)
message setMaxTurns(max: int)
message startMsg()
message stopMsg()
message pingMsg()
message pongMsg()

actor Ping() {
  mut int count = 0;
  mut int maxTurns = 5;
  mut actor<Pong> pong;

  def incrementAndPrint() => unit = {
    count = count + 1;
    println("ping");
  }

  receive = {
    | setPong(myPong: actor<Pong>) => {
        pong = myPong;
      }
    | setMaxTurns(max: int) => {
        maxTurns = max;
      }
    | startMsg() => {
        incrementAndPrint();
        message<pingMsg>() |> pong;
      }
    | pongMsg() => {
        incrementAndPrint();

        if (count > maxTurns) {
            println("ping stopped");
            message<stopMsg>() |> pong;
        } else {
            message<pingMsg>() |> pong;
        }
      }
  }
}

actor Pong() {
  mut actor<Ping> ping;

  receive = {
    | setPing(myPing: actor<Ping>) => {
          ping = myPing;
      }
    | pingMsg() => {
        println("  pong");
        message<pongMsg>() |> ping;
      }
    | stopMsg() => {
        println("pong stopped");
      }
  }
}


def main() => unit = {
  actor<Ping> ping = spawn actor<Ping>();
  actor<Pong> pong = spawn actor<Pong>();

  message<setPong>(pong) |> ping;
  message<setMaxTurns>(3) |> ping;

  message<setPing>(ping) |> pong;

  message<startMsg>() |> ping;
}
